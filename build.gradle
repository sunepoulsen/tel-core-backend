subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    group 'dk.sunepoulsen.tech-enterprise-labs'
    version "${project.property('tel-core-backend.version')}"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url = "${System.properties['maven.repository.base.url']}" + (
                    version.endsWith('SNAPSHOT') ?
                        "${System.properties['maven.repository.snapshots']}" :
                        "${System.properties['maven.repository.releases']}"
                )
                credentials {
                    username = "${System.properties['maven.repository.username']}"
                    password = "${System.properties['maven.repository.password']}"
                }
            }
        }
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    task allWrappers(type: Wrapper) {
        gradleVersion = "${project.property('gradle.version')}"
    }

    dependencies {
        // Lombok
        compileOnly "org.projectlombok:lombok:${project.property('lombok.version')}"
        annotationProcessor "org.projectlombok:lombok:${project.property('lombok.version')}"

        // Logging
        implementation "ch.qos.logback:logback-classic:${project.property('logback.version')}"
        implementation "org.slf4j:log4j-over-slf4j:${project.property('log4j-over-slf4j.version')}"

        // JUnit
        testCompile "junit:junit:${project.property('junit.version')}"

        // Spock
        testImplementation localGroovy() // Note: using the groovy implementation shipped with gradle
        testImplementation "org.spockframework:spock-core:${project.property('spock-core.version')}"
        testImplementation "cglib:cglib-nodep:${project.property('cglib-nodep.version')}"
        testImplementation "org.objenesis:objenesis:${project.property('objenesis.version')}"
    }

}
